plugins {
  id "com.github.ben-manes.versions" version "0.22.0"
  id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

nexusPublishing {
  repositories {
    sonatype {
      nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
      snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
      username = findProperty('sonatype.username')
      password = findProperty('sonatype.password')
    }
  }
}

def baseJvmArgs = [
  "--add-opens=java.base/java.io=ALL-UNNAMED",
  "--add-opens=java.base/java.lang=ALL-UNNAMED",
  "--add-opens=java.base/java.lang.invoke=ALL-UNNAMED",
  "--add-opens=java.base/java.lang.reflect=ALL-UNNAMED",
  "--add-opens=java.base/java.nio=ALL-UNNAMED",
  "--add-opens=java.base/java.util=ALL-UNNAMED",
  "--add-opens=java.base/java.util.concurrent.atomic=ALL-UNNAMED",
  "--add-opens=java.base/sun.nio.ch=ALL-UNNAMED",
  "-XX:-MaxFDLimit"
]
project.ext.set("baseJvmArgs", baseJvmArgs)

allprojects {
  apply plugin: "java-library"
  apply plugin: "jacoco"
  apply plugin: 'maven-publish'
  apply plugin: 'signing'

  group = "com.obsidiandynamics.jackdaw"
  version = "1.38.0-SNAPSHOT"
  
  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  //TODO remove when Javadoc errors have been resolved
  javadoc.options.addStringOption('Xdoclint:none', '-quiet')

  tasks.withType(JavaCompile) {
    configure(options) {
      options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
    }
  }

  repositories {
    mavenCentral()
    maven {
      url "https://oss.sonatype.org/content/repositories/snapshots"
    }
  }

  java {
    withJavadocJar()
    withSourcesJar()
  }

  signing {
    sign publishing.publications
  }
  
  ext {
    assertjVersion = "3.13.2"
    equalsverifierVersion = "3.1.9"
    fulcrumVersion = "0.39.0"
    junitVersion = "4.12"
    kafkaVersion = "2.3.0"
    log4jVersion = "1.2.17"
    mockitoVersion = "4.6.1"
    pojotesterVersion = "0.9.0"
    slf4jVersion = "1.7.25"
    yconfVersion = "0.23.0"
    zerologVersion = "0.33.0"
  }
  
  dependencies {
    compileOnly "org.apache.kafka:kafka-clients:${kafkaVersion}"
  
    testImplementation "com.obsidiandynamics.fulcrum:fulcrum-assert:${fulcrumVersion}"
    testImplementation "com.obsidiandynamics.fulcrum:fulcrum-await:${fulcrumVersion}"
    testImplementation "com.obsidiandynamics.fulcrum:fulcrum-junit:${fulcrumVersion}"
    testImplementation "com.obsidiandynamics.fulcrum:fulcrum-verifier:${fulcrumVersion}"
    testImplementation "com.obsidiandynamics.yconf:yconf-snakeyaml:${yconfVersion}"
    testImplementation "junit:junit:${junitVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.apache.kafka:kafka-clients:${kafkaVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "com.obsidiandynamics.pojotester:core:${pojotesterVersion}"
    testImplementation "nl.jqno.equalsverifier:equalsverifier:${equalsverifierVersion}"

    testImplementation "com.obsidiandynamics.zerolog:zerolog-slf4j17:${zerologVersion}"
    testImplementation "org.slf4j:slf4j-api:${slf4jVersion}"
    testImplementation "org.slf4j:slf4j-log4j12:${slf4jVersion}"
    testImplementation "log4j:log4j:${log4jVersion}"
  }
  
  test {
    exclude "**/*IT.class"
    jvmArgs += baseJvmArgs
  }
  
  task integrationTest(type: Test, description: "Runs integration tests", group: "Verification") {
    include "**/*IT.class"
    jvmArgs += baseJvmArgs
  }
  
  jacoco {
    toolVersion = "0.8.8"
  }
  
  task verify {
    dependsOn test
    dependsOn integrationTest
  }

  jacocoTestReport {
    additionalSourceDirs.from = files(sourceSets.main.allSource.srcDirs)
    sourceDirectories.from = files(sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(sourceSets.main.output)
    executionData.from = files("$buildDir/jacoco/test.exec") + file("$buildDir/jacoco/integrationTest.exec")
    reports {
      html.required = true
      xml.required = true
      csv.required = false
    }
  }
}

subprojects {
  dependencies {
    testImplementation project(":").sourceSets.test.output
  }
}

task jacocoRootReport(type: JacocoReport) {
  mustRunAfter = allprojects.test + allprojects.integrationTest + allprojects.javadoc
  additionalSourceDirs.from = files(allprojects.sourceSets.main.allSource.srcDirs)
  sourceDirectories.from = files(allprojects.sourceSets.main.allSource.srcDirs)
  classDirectories.from = files(allprojects.sourceSets.main.output)
  executionData.setFrom project.fileTree(dir: '.', include: ['**/build/jacoco/test.exec', '**/build/jacoco/integrationTest.exec'])
  reports {
    html.required = true
    xml.required = true
    csv.required = false
  }
  onlyIf = {
    true
  }
  afterEvaluate {
    classDirectories.from = files(classDirectories.files.collect {
      fileTree(dir: it, exclude: ["sample/**", "**/*Uncovered*"])
    })
  }
}

def packageName = "jackdaw-core"
def packageDescription = "Configuration support and other core Jackdaw elements"

dependencies {
  api "com.obsidiandynamics.fulcrum:fulcrum-nodequeue:${fulcrumVersion}"
  api "com.obsidiandynamics.fulcrum:fulcrum-props:${fulcrumVersion}"
  api "com.obsidiandynamics.fulcrum:fulcrum-retry:${fulcrumVersion}"
  api "com.obsidiandynamics.fulcrum:fulcrum-worker:${fulcrumVersion}"
  api "com.obsidiandynamics.yconf:yconf-core:${yconfVersion}"
  api "com.obsidiandynamics.zerolog:zerolog-core:${zerologVersion}"
}

jar {
  finalizedBy jacocoRootReport
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = packageName
      from components.java
      pom {
        name = packageName
        description = packageDescription
        url = 'https://github.com/obsidiandynamics/jackdaw'
        licenses {
          license {
            name = 'BSD 3-Clause License'
            url = 'https://opensource.org/licenses/BSD-3-Clause'
          }
        }
        developers {
          developer {
            id = 'ekoutanov'
            name = 'Emil Koutanov'
            email = 'ekoutanov@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/obsidiandynamics/jackdaw.git'
          developerConnection = 'scm:git:ssh://github.com/obsidiandynamics/jackdaw.git'
          url = 'https://github.com/obsidiandynamics/jackdaw'
        }
      }
    }
  }
}
